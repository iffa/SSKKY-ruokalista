apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
try {
    props.load(file('app.properties').newDataInputStream())
} catch (Exception ex) {
    throw ex;
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('HH.mm:ss dd.MM.yyyy')
    return formattedDate
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def versionMajor = 3
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 1 // If you're happy and you know it, bump this

    defaultConfig {
        applicationId "email.crappy.ssao.ruoka"
        minSdkVersion 16
        targetSdkVersion 23

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            // TODO: Change to match release keys
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword 'storePass'
            keyPassword 'keyPass'
        }
        debug {
            storeFile file("keystore/debug.keystore")
            keyAlias "debug"
            storePassword "android"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false // TODO: ProGuard rules
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix ' (Debug)'
            applicationIdSuffix '.debug'
            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version", variant.versionName + " - " + getDate()
        variant.resValue "string", "ad_unit_id", "\"${props.getProperty("adUnitId")}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'
        warning 'ResourceType' // Keep lint happy about ButterKnife
    }
}

dependencies {
    final SUPPORT_LIB_VERSION = '23.2.1'
    final DAGGER_VERSION = '2.0.2'
    final RETROFIT_VERSION = '2.0.0'
    final ANNOTATED_ADAPTER_VERSION = '1.1.1'

    // Disable inspection due to bug in lint
    //noinspection GradleDependency
    compile "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    //noinspection GradleDependency
    compile "com.android.support:cardview-v7:$SUPPORT_LIB_VERSION"
    //noinspection GradleDependency
    compile "com.android.support:design:$SUPPORT_LIB_VERSION"
    compile 'com.google.android.gms:play-services-ads:8.4.0'

    compile 'com.jakewharton.timber:timber:4.1.1'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compile 'org.glassfish:javax.annotation:10.0-b28' // Required by Dagger

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"

    compile "io.reactivex:rxjava:1.1.1"
    compile "io.reactivex:rxandroid:1.1.0"

    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'

    compile 'com.jakewharton:butterknife:7.0.1'

    compile "com.hannesdorfmann.annotatedadapter:annotation:$ANNOTATED_ADAPTER_VERSION"
    compile "com.hannesdorfmann.annotatedadapter:support-recyclerview:$ANNOTATED_ADAPTER_VERSION"
    apt "com.hannesdorfmann.annotatedadapter:processor:$ANNOTATED_ADAPTER_VERSION"

    compile 'net.xpece.android:support-preference:0.5.11'

    compile 'com.code-troopers.betterpickers:library:2.5.2'
    compile 'com.github.ybq:AndroidSpinKit:v1.0.0'

    compile 'in.workarounds.typography:typography:0.0.8'
}
