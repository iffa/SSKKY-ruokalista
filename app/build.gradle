apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('dd.MM.yyyy HH.mm')
    return formattedDate
}

Properties props = new Properties()
try {
    props.load(file('../local.properties').newDataInputStream())
} catch (Exception ex) {
    throw ex;
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    def versionMajor = 4
    def versionMinor = 0
    def versionPatch = 4
    def versionBuild = 0 // If you're happy and you know it, bump this

    defaultConfig {
        applicationId "email.crappy.ssao.ruoka"
        minSdkVersion 19
        targetSdkVersion 24

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "v${versionMajor}.${versionMinor}.${versionPatch} - " + getDate()
    }

    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
            keyAlias "debug"
            storePassword "android"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false // TODO: https://github.com/google/dagger/issues/298
            shrinkResources false // TODO: Wait for fix in Gradle plugin
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'

            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "ad_bottom", "\"${props.getProperty("ad_id_bottom")}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def supportVersion = '24.2.0'
    def daggerVersion = '2.6.1'
    def retrofitVersion = '2.1.0'
    def butterknifeVersion = '8.4.0'
    def leakcanaryVersion = '1.4-SNAPSHOT'
    def annotatedAdapterVersion = '1.1.1'
    def firebaseVersion = '9.4.0'

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:transition:$supportVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile 'org.glassfish:javax.annotation:10.0-b28' // Required by Dagger

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile 'com.squareup.moshi:moshi-adapters:1.2.0' // To support Date objects

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile "com.hannesdorfmann.annotatedadapter:annotation:$annotatedAdapterVersion"
    compile "com.hannesdorfmann.annotatedadapter:support-recyclerview:$annotatedAdapterVersion"
    apt "com.hannesdorfmann.annotatedadapter:processor:$annotatedAdapterVersion"

    compile "io.reactivex:rxjava:1.1.10"
    compile 'com.artemzin.rxjava:proguard-rules:1.1.9.0'
    compile "io.reactivex:rxandroid:1.2.1"
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'

    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'

    compile "com.mikepenz:materialize:1.0.0@aar"

    compile 'net.xpece.android:support-preference:1.0.2'

    compile 'com.github.prashantsolanki3:Shoot:0.8.3'

    //
    // Debugging, crash reporting, analytics etc.
    //

    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-ads:$firebaseVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.9@aar') {
        transitive = true;
    }

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
}

apply plugin: 'com.google.gms.google-services'