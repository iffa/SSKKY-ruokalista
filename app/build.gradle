apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('dd.MM.yyyy HH.mm')
    return formattedDate
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    def versionMajor = 4
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 0 // If you're happy and you know it, bump this

    defaultConfig {
        applicationId "email.crappy.ssao.ruoka"
        minSdkVersion 19
        targetSdkVersion 24

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "v${versionMajor}.${versionMinor}.${versionPatch}" + " - " + getDate()
    }

    signingConfigs {
        release {
            // TODO: Change to match release keys
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword 'storePass'
            keyPassword 'keyPass'
        }

        debug {
            storeFile file("keystore/debug.keystore")
            keyAlias "debug"
            storePassword "android"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false // TODO: ProGuard rules
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            versionNameSuffix ' (Debug)'
            applicationIdSuffix '.debug'

            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def supportVersion = '24.1.1'
    def daggerVersion = '2.6'
    def retrofitVersion = '2.1.0'
    def butterknifeVersion = '8.2.1'
    def leakcanaryVersion = '1.4-SNAPSHOT'
    def annotatedAdapterVersion = '1.1.1'

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile 'org.glassfish:javax.annotation:10.0-b28' // Required by Dagger

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile 'com.squareup.moshi:moshi-adapters:1.2.0' // To support Date objects

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile "com.hannesdorfmann.annotatedadapter:annotation:$annotatedAdapterVersion"
    compile "com.hannesdorfmann.annotatedadapter:support-recyclerview:$annotatedAdapterVersion"
    apt "com.hannesdorfmann.annotatedadapter:processor:$annotatedAdapterVersion"

    compile "io.reactivex:rxjava:1.1.9"
    compile "io.reactivex:rxandroid:1.2.1"
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    compile 'com.jakewharton.timber:timber:4.2.0'
    compile 'net.ypresto.timbertreeutils:timbertreeutils:1.0.0'

    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'

    compile "com.mikepenz:materialize:0.9.0@aar"

    compile 'net.xpece.android:support-preference:1.0.2'

    compile 'com.afollestad.material-dialogs:core:0.9.0.1'

    compile 'com.yqritc:recyclerview-flexibledivider:1.4.0'

    //
    // Debugging, crash reporting, analytics etc.
    //

    compile 'com.google.firebase:firebase-messaging:9.4.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.8@aar') {
        transitive = true;
    }

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
}

apply plugin: 'com.google.gms.google-services'