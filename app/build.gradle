apply plugin: 'io.github.rockerhieu.versionberg'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

/*
versionberg {
    major 4
    minor 2
    patch 0
    nameTemplate '${major}.${minor}.${commitCount}.${commitSha}'
    codeTemplate '(((${major} * 100) + ${minor}) * 100) * 100000 + ${build}'
}
*/

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Exception e) {
    throw e;
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "email.crappy.ssao.ruoka"

        minSdkVersion 19
        targetSdkVersion 25

        versionCode 10000
        versionName "4.2.0"
    }

    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
            keyAlias "debug"
            storePassword "android"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false // TODO: https://github.com/google/dagger/issues/298
            shrinkResources false // TODO: Wait for fix in Gradle plugin
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'

            debuggable true
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "ad_id_bottom", "\"${properties.getProperty("ad_id_bottom")}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput "stdout"
        checkAllWarnings true
        showAll true
        explainIssues true
    }
}

dependencies {
    def supportVersion = '25.3.1'
    def daggerVersion = '2.10'
    def thirtyinchVersion = '0.8.0-rc3'
    def retrofitVersion = '2.2.0'
    def butterknifeVersion = '8.5.1'
    def leakcanaryVersion = '1.5'
    def annotatedAdapterVersion = '1.1.1'
    def firebaseVersion = '10.2.1'

    //noinspection GradleCompatible
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:transition:$supportVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile 'org.glassfish:javax.annotation:10.0-b28' // Required by Dagger

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile 'com.squareup.moshi:moshi-adapters:1.4.0' // To support Date objects

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    compile "com.hannesdorfmann.annotatedadapter:annotation:$annotatedAdapterVersion"
    compile "com.hannesdorfmann.annotatedadapter:support-recyclerview:$annotatedAdapterVersion"
    apt "com.hannesdorfmann.annotatedadapter:processor:$annotatedAdapterVersion"

    compile "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion"
    compile "net.grandcentrix.thirtyinch:thirtyinch-rx:$thirtyinchVersion"
    compile "net.grandcentrix.thirtyinch:thirtyinch-plugin:$thirtyinchVersion"

    // These are always behind on the version number
    compile "com.pascalwelsch.compositeandroid:activity:25.3.0"
    compile "com.pascalwelsch.compositeandroid:fragment:25.3.0"

    compile "io.reactivex:rxjava:1.2.9"
    compile 'com.artemzin.rxjava:proguard-rules:1.2.5.0'
    compile 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    compile "io.reactivex:rxandroid:1.2.1"
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'
    compile 'com.github.lukaspili.reactive-billing:reactive-billing:0.2@aar'

    compile 'com.jakewharton.timber:timber:4.5.1'

    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'

    compile "com.mikepenz:materialize:1.0.0@aar"

    compile 'net.xpece.android:support-preference:1.2.7'

    compile 'com.github.prashantsolanki3:Shoot:0.8.3'

    //
    // Debugging, crash reporting, analytics etc.
    //

    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-ads:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
}

apply plugin: 'com.google.gms.google-services'